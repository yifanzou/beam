apply plugin: 'groovy'
apply plugin: 'maven'

ext {
    jobDslVersion = '1.67'
    jenkinsVersion = '2.5'
}

sourceSets {
    jobs {
        groovy {
            srcDirs '.test-infra/jenkins'
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.8'
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.33'
    testCompile("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}") {
         exclude group: 'org.jenkins-ci.ui', module: 'bootstrap' // https://github.com/sheehan/job-dsl-gradle-example/issues/87
    }

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.9@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:5.12@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.31.4'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.12'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    testPlugins 'org.jenkins-ci.plugins:matrix-project:1.12'
    testPlugins 'org.jenkins-ci.main:maven-plugin:3.1'

    testPlugins 'org.jenkins-ci.plugins:build-timeout:1.19'
    testPlugins 'org.jenkins-ci.plugins:envinject:2.1.5'
    testPlugins 'org.jenkins-ci.plugins:credentials-binding:1.14'
    testPlugins 'org.jenkins-ci.plugins:mailer:1.20'
    testPlugins 'org.jenkins-ci.plugins:junit:1.23'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'build-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast {
        println "-----------------" + sourceSets.test.output.resourcesDir +"------------------------"
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.')-1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins build harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}
